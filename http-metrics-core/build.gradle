dependencies {
    compile group: 'com.jonnymatts.prometheus', name: 'metrics-core', version: "0.1.0"
    compile group: 'io.prometheus', name: 'simpleclient', version: "${prometheus_client_version}"
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: "${javax_version}"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jackson_version}"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jackson_version}"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "${jackson_version}"
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: "${jackson_version}"
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
}

ext {
    jarName = 'http-metrics-core'
}

jar {
    baseName jarName
    manifest {
        attributes "application-version": version
    }
}

javadoc {
    source = sourceSets.main.allJava
}

task sourcesJar(type: Jar, dependsOn: classes) {
    baseName jarName
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    baseName jarName
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

// signing and maven plugin configuration. Run gradle with project property -Prelease to use these tasks
// Ensure gradle.properties are set for signing key and sonatype credentials
if (project.hasProperty("release")) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'Prometheus HTTP Metrics'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    artifactId jarName
                    description 'HTTP metrics exporter for Prometheus'
                    url 'jonnymatts.com'

                    scm {
                        connection 'scm:git:git://github.com/jonnymatts/prometheus-http-metrics.git'
                        developerConnection 'scm:git:ssh://github.com:jonnymatts/prometheus-http-metrics.git'
                        url 'http://github.com/jonnymatts/prometheus-http-metrics/tree/master'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            name 'Jonathan Matts'
                            email 'jonoamatts@gmail.com'
                        }
                    }
                }
            }
        }
    }
}